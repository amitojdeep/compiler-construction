<mainFunction> = MAIN SQO SQC <stmtsAndFunctionDefs> END 
<stmtsAndFuntionDefs> = <stmtOrFunctionDef> <newStmts> 
<newStmts> = <stmtsAndFunctionDefs> | EPS 
<stmtOrFunctionDef> = <stmt> | <functionDef> 
<stmt> = <declarationStmt> | <assignmentStmt_type1> | <assignmentStmt_type2> | <ifStmt> | <ioStmt> | <funCallStmt> SEMICOLON 
<functionDef> = FUNCTION SQO <parameter_list> SQC ASSIGNOP FUNID SQO <parameter_list> SQC <stmtsAndFunctionDefs> END SEMICOLON 
<parameter_list> = <type> ID <remainingList> 
<type> = INT | REAL | STRING | MATRIX 
<remainingList> = COMMA <parameter_list> | EPS 
<declarationStmt> = <type> <var_list> SEMICOLON 
<var_list> = ID <more_ids> 
<more_ids> = COMMA <var_list> | EPS 
<assignmentStmt_type1> = <leftHandSide_singleVar> ASSIGNOP <rightHandSide_type1> SEMICOLON 
<assignmentStmt_type2> = <leftHandSide_listVar> ASSIGNOP <rightHandSide_type2> SEMICOLON 
<leftHandSide_singleVar> = ID 
<leftHandSide_listVar> = SQO <var_list> SQC 
<rightHandSide_type1> = <arithmeticExpression> | <sizeExpression> | <funCallStmt> 
<rightHandSide_type2> = <sizeExpression> | <funCallStmt> 
<sizeExpression> = SIZE ID 
<ifStmt> = IF OP <booleanExpression> CL <stmt> <otherStmts> <newIf> 
<newIf> = ELSE <stmt> <otherStmts> ENDIF SEMICOLON | ENDIF SEMICOLON 
<otherStmts> = <stmt> <otherStmts> | EPS 
<ioStmt> = READ OP ID CL SEMICOLON | PRINT OP ID CL SEMICOLON 
<funCallStmt> = FUNID OP <inputParameterList> CL 
<inputParameterList> = <var> <listVar> | EPS 
<listVar> = COMMA <inputParameterList> | EPS 
<arithmeticExpression> = <arithmeticTerm> <newarithmeticExpression> 
<newarithmeticExpression> = <operator_lowPrecedence> <arithmeticExpression> | EPS 
<arithmeticTerm> = <factor> <newarithmeticTerm> 
<newarithmeticTerm> = <operator_highPrecedence> <arithmeticTerm> | EPS  
<factor> = OP <arithmeticExpression> CL | <var> 
<operator_lowPrecedence> = PLUS | MINUS 
<operator_highPrecedence> = MUL | DIV 
<booleanExpression> = OP <booleanExpression> CL <logicalOp> OP <booleanExpression> CL | <constrainedVars> <relationalOp> <constrainedVars> | NOT OP <booleanExpression> CL 
<constrainedVars> = ID | NUM | RNUM 
<var> = ID <matrixElement> | NUM | RNUM | STR | <matrix> 
<matrix> = SQO <rows> SQC 
<rows> = <row> <newRows> 
<newRows> = SEMICOLON <rows> | EPS 
<row> = NUM <remainingColElements> 
<remainingColElements> = COMMA NUM <remainingColElements> | EPS 
<matrixElement> = SQO NUM COMMA NUM SQC | EPS 
<logicalOp> = AND | OR 
<relationalOp> = LT | LE | EQ | GT | GE | NE 